<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>Hello Terrain </title>
<meta charset="utf-8"> 
</head>

    
<script id="shader-vs" type="x-shader/x-vertex">
   attribute vec3 aVertexNormal;
   attribute vec3 aVertexPosition;
   attribute float aHeight;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    // uMin and uMax get the minHeight and maxHeight
    uniform float uMin;
    uniform float uMax;
    
    varying vec3 vNormal;
    varying vec3 vPosition;
    varying vec3 vHeightColor;

    void main(void) {
    
    // Get the vertex position in eye coordinates
    vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
    vPosition = vertexPositionEye4.xyz / vertexPositionEye4.w;
        
    // Transform the normal (n) to eye coordinates
    vNormal = normalize(uNMatrix * aVertexNormal);
    
    // Normalize the height to the range [0, 1]
    float normalHeight = (aVertexPosition[2]-uMin) / (uMax-uMin);
    
    // Set different color according to the height
    if(normalHeight <0.2)
        vHeightColor = vec3(0.37,0.13,0.06);
    else if(normalHeight < 0.4)
        vHeightColor = vec3(0.62,0.29,0.12);
    else if(normalHeight < 0.6)
        vHeightColor = vec3(0.92,0.68,0.29);
    else if(normalHeight < 0.8)
        vHeightColor = vec3(0.97,0.87,0.45);
    else 
        vHeightColor = vec3(1.0,1.0,0.8);
    
    gl_Position = uPMatrix*uMVMatrix*vec4(aVertexPosition, 1.0);
        
    }
</script>
  
<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;
    uniform vec3 uLightPosition;
    uniform vec3 uSpecularLightColor;

    varying vec3 vNormal;
    varying vec3 vPosition;
    varying vec3 vHeightColor;

    const float shininess = 32.0;
    void main(void) {

        vec4 vColor;

        // Calculate the vector (l) to the light source
        vec3 vectorToLightSource = normalize(uLightPosition - vPosition);
        // Calculate n dot l for diffuse lighting
        float diffuseLightWeightning = max(dot(vNormal, 
                                        vectorToLightSource), 0.0);
                                        
        // Calculate the reflection vector (r) that is needed for specular light
        vec3 reflectionVector = normalize(reflect(-vectorToLightSource, 
                                                vNormal));
        
        // The camera in eye coordinates is located in the origin and is pointing
        // along the negative z-axis. Calculate viewVector (v) 
        // in eye coordinates as:
        // (0.0, 0.0, 0.0) - vertexPositionEye3
        vec3 viewVectorEye = -normalize(vPosition);
        
        //float rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);
        vec3 h = vectorToLightSource + viewVectorEye;
        h = normalize(h);
        float ndoth = max(dot(vNormal,h), 0.0);

        float specularLightWeightning = pow(ndoth, shininess);

        vec3 uAmbientLightColor = vec3(vHeightColor.x/10.0,vHeightColor.y/10.0,vHeightColor.z/10.0);

        vColor = vec4((uAmbientLightColor
                   + vHeightColor * diffuseLightWeightning
                    + uSpecularLightColor * specularLightWeightning),1.0);

        // Add fog to terrain
        vec4 fogColor = vec4(1.0, 1.0, 1.0, 1.0);        
        const float LOG2 = 1.442695;
        float z = gl_FragCoord.z / gl_FragCoord.w * 900.0;
        float fogDensity = 0.0005;        
        float fogFactor = exp2( -1.0 * fogDensity * fogDensity * z * z * LOG2 ); 
        fogFactor = clamp(fogFactor, 0.0, 1.0);
        
        gl_FragColor = mix(fogColor, vColor, fogFactor );
        
 }
</script>

<script src="gl-matrix-min.js"></script> 
<script src="webgl-utils.js"></script>
<script src="terrainModeling.js"></script>  
<script src="HelloTerrain.js"></script>   
<style>
    canvas{
        vertical-align: top;
        display: inline-block;
    }
    .introduction{
        vertical-align: top;
        display: inline-block;
        border: 1px solid red;
        padding: 10px;

    }
</style>
<body onload="startup();">
 
    <canvas id="myGLCanvas" width="800" height="800"></canvas>
 
    <div class="introduction">
        <h3>Control Row: left arrow and right arrow</h3>
        <h3>Control Pitch: up arrow and down arrow</h3>
        <h3>Control Speed: '+' and '-'</h3>
    </div>

</body>

</html>
